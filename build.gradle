buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

project(':clutz') {
    apply plugin: 'com.github.johnrengelman.shadow'

    def getRevision = {
        def proc = 'git rev-parse --short=7 HEAD'.execute([], projectDir)
        proc.waitFor()
        (proc.exitValue() == 0) ? proc.text.trim() : '-'
    }

    afterEvaluate {
        // for application plugin
        mainClassName = 'com.google.javascript.clutz.DeclarationGenerator'

        shadowJar {
            // output filename: clutz-all.jar
            version = null
        }

        def closureVersion = configurations.compile.getAllDependencies().find {
            it.group == 'com.google.javascript' && it.name == 'closure-compiler'
        }.version
        version = version + ':' + getRevision() + ':' + closureVersion
    }

    task showVersion {
        doLast {
            println version
        }
    }
}
